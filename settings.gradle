import static groovy.io.FileType.DIRECTORIES
import static groovy.io.FileVisitResult.SKIP_SUBTREE

pluginManagement {
	repositories {
		mavenCentral()
		gradlePluginPortal()
		maven {
			url 'https://repo.spring.io/release'
		}
	}
}

plugins {
	id "com.gradle.enterprise" version "3.10.2"
}

rootProject.name="Spring 6 Recipes - Sources"

include 'spring-6-recipes-dependencies'
include 'shared-resources'


def includeDirs = ~/^(?i)(ch|recipe|appendix).*$/
def preDir = {
    // Temporary exclude
    switch (it.name) {
        case 'ch05': // WebSecurityConfigurerAdapter
        case 'ch13': // ModelMap <-> Model
            return SKIP_SUBTREE
    }
    // For now exclude directories not working on to increase performance
    if (!includeDirs.matcher(it.name).matches()) {
        return SKIP_SUBTREE
    }
}

def getProjectName(String dir) {
    def projName = (dir - (rootDir.toString() + "/")).replaceAll('/', ':')
    if (projName == dir) {
        projName = (dir - (rootDir.toString() + "\\")).replaceAll('\\\\', ':')
    }
    return projName
}

file("${rootDir}").traverse( type: DIRECTORIES, preDir: preDir) { dir ->

    def dstr = dir.toString()

    if (dir.name.contains('recipe') || dir.name.contains('appendix')) {
        def projectName = getProjectName(dstr)
        include projectName
    }
}
